# https://www.sqlite.org/lang.html

root = statement_or_query semicolon? eof;
statement_or_query =
  | SELECT => select
  | VALUES => values
  | CREATE => create
  | INSERT => insert
  | UPDATE => update
  | DROP => drop;

select = SELECT distinct_or_all? result_columns from? where? group_by? having? window? order_by? limit?;
distinct_or_all =
  | DISTINCT
  | ALL;
result_columns = result_column*comma;
result_column = # TODO
  | result_expr
  | star
  | table_star; 
result_expr = expr (AS column_name)?;
table_star = table_name dot star;
from = FROM tables_or_subqueries_or_join*;
tables_or_subqueries_or_join =
  | tables_or_subqueries
  | join_clause;
tables_or_subqueries = table_or_subquery+comma;
table_or_subquery = 
  | table
  | subquery;
table = name binding?; # TODO
binding = AS name;
join_clause = TODO;
where = WHERE expr;
group_by = GROUP BY exprs;
having = HAVING expr;
window = WINDOW TODO;
order_by = ORDER BY ordering_terms;
ordering_terms = ordering_term+comma;
ordering_term = collate? asc_or_desc? nulls_first_or_last? expr;
collate = COLLATE collation_name;
collation_name = name; # TODO
asc_or_desc =
  | ASC
  | DESC;
nulls_first_or_last = NULLS first_or_last;
first_or_last = 
  | FIRST
  | LAST;
limit = LIMIT exprs; # TODO offset

values = VALUES row+comma;
row = open_paren exprs close_paren;

create =
  | create_table
  | create_index
  | create_view;
create_table = CREATE TEMP_OR_TEMPORARY? TABLE IF_NOT_EXISTS? table_name column_defs;
TEMP_OR_TEMPORARY = | TEMP | TEMPORARY;
IF_NOT_EXISTS = IF NOT EXISTS;
table_name = name;
column_name = name;
column_defs = open_paren column_def*comma close_paren;
column_def = column_name typ? column_constraint?;
create_index = CREATE UNIQUE? INDEX IF_NOT_EXISTS? index_name ON table_name open_paren indexed_column+comma close_paren (WHERE expr)?;
index_name = table_name;
indexed_column = column_name asc_or_desc?; # TODO
create_view = CREATE TEMP_OR_TEMPORARY? VIEW IF_NOT_EXISTS? table_name column_defs? AS select;

insert = INSERT INTO table_name column_names? values_or_select;
column_names = open_paren column_name*comma close_paren;
values_or_select =
  | VALUES => values
  | select;
typ = name typ_length?;
typ_length = open_paren number (comma number)? close_paren;
column_constraint = 
  | primary_key;
primary_key = PRIMARY KEY asc_or_desc?;

update = UPDATE table_name SET column_name equal expr update_from? update_where?; # TODO
update_from = TODO;
update_where = WHERE expr;

drop =
  | drop_table
  | drop_index
  | drop_view;
drop_table = DROP TABLE if_exists? table_name;
drop_index = DROP INDEX if_exists? table_name;
drop_view = DROP VIEW if_exists? table_name;
if_exists = IF EXISTS;


exprs = expr*comma;
expr = expr_or;
expr_or = expr_and=left ((OR expr_or)?)=right;
expr_and = expr_not=left ((AND expr_and)?)=right;
expr_not = NOT* expr_incomp;
expr_incomp = expr_comp=left ((op_incomp=op expr_incomp)?)=right;
expr_comp = expr_add=left ((op_comp=op expr_comp)?)=right;
expr_add = expr_mult=left ((op_add=op expr_add)?)=right;
expr_mult = expr_atom=left ((op_mult=op expr_atom)?)=right;

op_incomp = | equal | double_equal | not_equal | IS | IS_NOT | IS_DISTINCT_FROM | IS_NOT_DISTINCT_FROM | IN | MATCH | LIKE | REGEXP | GLOB | NOT_IN | NOT_MATCH | NOT_LIKE | NOT_REGEXP | NOT_GLOB;
NOT_IN = NOT IN;
NOT_MATCH = NOT MATCH;
NOT_LIKE = NOT LIKE;
NOT_REGEXP = NOT REGEXP;
NOT_GLOB = NOT GLOB;
op_comp = | less_than | greater_than | less_than_or_equal | greater_than_or_equal;
op_add = | plus | minus;
op_mult = | star | forward_slash | percent;

expr_atom =
  | CASE => case
  | subquery_prefix => subquery
  | open_paren => subexpr
  | function_call
  | table_column_ref
  | column_ref
  | value;

expr_incomp_complex = 
  | expr_incomp_between
  | expr_incomp_not_binop;
expr_incomp_between = expr_comp=left NOT? BETWEEN expr=start AND expr=end;
IS_NOT = IS NOT;
IS_DISTINCT_FROM = IS DISTINCT FROM;
IS_NOT_DISTINCT_FROM = IS NOT DISTINCT FROM;
expr_incomp_not_binop = expr_comp=left NOT? (| IN | MATCH | LIKE | REGEXP | GLOB)=op expr_incomp=right;
expr_incomp_postop = expr_comp=left (| ISNULL | NOTNULL | NOT_NULL)=op;
NOT_NULL = NOT NULL;

column_ref = column_name;
table_column_ref = table_name dot column_name;

subquery_prefix = exists_or_not_exists? open_paren SELECT;
subquery = exists_or_not_exists? open_paren select close_paren;
exists_or_not_exists =
  | EXISTS
  | NOT_EXISTS;
NOT_EXISTS = NOT EXISTS;

subexpr = open_paren expr close_paren;

case = CASE expr? case_when* case_else? END;
case_when = WHEN expr=when THEN expr=then;
case_else = ELSE expr;
function_call = function_name open_paren function_args? close_paren; # TODO filter/over
function_name = name; # TODO
function_args =
  | (DISTINCT? expr+comma)=args
  | star;
value =
  | number
  | string
  | NULL;